/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/

import analysis.normed_space.basic
import topology.metric_space.hausdorff_distance

/-!
# Properties of pointwise addition and scalar multiplication of sets in normed spaces.

We explore the relationships between pointwise addition and scalar multiplication of sets in
vector spaces, and the norm. Notably, we express arbitrary balls as rescaling of other balls,
and we show that the sum or multiplication of bounded sets remain bounded.
-/

open metric set
open_locale pointwise topological_space

section semi_normed_group

variables {E : Type*} [semi_normed_group E]

lemma metric.bounded.add
  {s t : set E} (hs : bounded s) (ht : bounded t) :
  bounded (s + t) :=
begin
  obtain ‚ü®Rs, hRs‚ü© : ‚àÉ (R : ‚Ñù), s ‚äÜ closed_ball 0 R := hs.subset_ball 0,
  obtain ‚ü®Rt, hRt‚ü© : ‚àÉ (R : ‚Ñù), t ‚äÜ closed_ball 0 R := ht.subset_ball 0,
  refine (bounded_iff_subset_ball 0).2 ‚ü®Rs + Rt, _‚ü©,
  rintros z ‚ü®x, y, hx, hy, rfl‚ü©,
  rw mem_closed_ball_zero_iff,
  calc ‚à•x + y‚à• ‚â§ ‚à•x‚à• + ‚à•y‚à• : norm_add_le _ _
  ... ‚â§ Rs + Rt :
    add_le_add (mem_closed_ball_zero_iff.1 (hRs hx)) (mem_closed_ball_zero_iff.1 (hRt hy))
end

@[simp] lemma singleton_add_ball (x y : E) (r : ‚Ñù) :
  {x} + ball y r = ball (x + y) r :=
by simp only [preimage_add_ball, image_add_left, singleton_add, sub_neg_eq_add, add_comm y x]

lemma singleton_add_ball_zero (x : E) (r : ‚Ñù) :
  {x} + ball 0 r = ball x r :=
by simp

@[simp] lemma singleton_add_closed_ball (x y : E) (r : ‚Ñù) :
  {x} + closed_ball y r = closed_ball (x + y) r :=
by simp only [add_comm y x, preimage_add_closed_ball, image_add_left, singleton_add, sub_neg_eq_add]

lemma singleton_add_closed_ball_zero (x : E) (r : ‚Ñù) :
  {x} + closed_ball 0 r = closed_ball x r :=
by simp

lemma is_compact.cthickening_eq_add_closed_ball
  {s : set E} (hs : is_compact s) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  cthickening r s = s + closed_ball 0 r :=
begin
  rcases eq_empty_or_nonempty s with rfl|hne, { simp only [cthickening_empty, empty_add] },
  apply subset.antisymm,
  { assume x hx,
    obtain ‚ü®y, ys, hy‚ü© : ‚àÉ y ‚àà s, emetric.inf_edist x s = edist x y :=
      hs.exists_inf_edist_eq_edist hne,
    have D1 : edist x y ‚â§ ennreal.of_real r := (le_of_eq hy.symm).trans hx,
    have D2 : dist x y ‚â§ r,
    { rw edist_dist at D1,
      exact (ennreal.of_real_le_of_real_iff hr).1 D1 },
    refine set.mem_add.2 ‚ü®y, x - y, ys, _, add_sub_cancel'_right _ _‚ü©,
    simpa only [dist_eq_norm, mem_closed_ball_zero_iff] using D2 },
  { assume x hx,
    rcases set.mem_add.1 hx with ‚ü®y, z, ys, hz, rfl‚ü©,
    apply mem_cthickening_of_dist_le (y + z) y _ _ ys,
    simpa only [dist_eq_norm, add_sub_cancel', mem_closed_ball_zero_iff] using hz }
end

end semi_normed_group

section semi_normed_space

variables {ùïú : Type*} [normed_field ùïú] {E : Type*} [semi_normed_group E] [semi_normed_space ùïú E]

theorem smul_ball {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
  c ‚Ä¢ ball x r = ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hc,
  conv_lhs { rw ‚Üêinv_smul_smul‚ÇÄ hc x },
  simp [‚Üê div_eq_inv_mul, div_lt_iff (norm_pos_iff.2 hc), mul_comm _ r, dist_smul],
end

theorem smul_closed_ball' {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
  c ‚Ä¢ closed_ball x r = closed_ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hc,
  conv_lhs { rw ‚Üêinv_smul_smul‚ÇÄ hc x },
  simp [dist_smul, ‚Üê div_eq_inv_mul, div_le_iff (norm_pos_iff.2 hc), mul_comm _ r],
end

lemma metric.bounded.smul {s : set E} (hs : bounded s) (c : ùïú) :
  bounded (c ‚Ä¢ s) :=
begin
  obtain ‚ü®R, Rpos, hR‚ü© : ‚àÉ (R : ‚Ñù), 0 < R ‚àß s ‚äÜ closed_ball 0 R := hs.subset_ball_lt 0 0,
  refine (bounded_iff_subset_ball 0).2 ‚ü®‚à•c‚à• * R, _‚ü©,
  assume z hz,
  obtain ‚ü®y, ys, rfl‚ü© : ‚àÉ (y : E), y ‚àà s ‚àß c ‚Ä¢ y = z := mem_smul_set.1 hz,
  simp only [mem_closed_ball_zero_iff],
  calc ‚à•c ‚Ä¢ y‚à• = ‚à•c‚à• * ‚à•y‚à• : norm_smul _ _
  ... ‚â§ ‚à•c‚à• * R : mul_le_mul_of_nonneg_left (mem_closed_ball_zero_iff.1 (hR ys)) (norm_nonneg _)
end

/-- If `s` is a bounded set, then for small enough `r`, the set `{x} + r ‚Ä¢ s` is contained in any
fixed neighborhood of `x`. -/
lemma eventually_singleton_add_smul_subset
  {x : E} {s : set E} (hs : bounded s) {u : set E} (hu : u ‚àà ùìù x) :
  ‚àÄ·∂† r in ùìù (0 : ùïú), {x} + r ‚Ä¢ s ‚äÜ u :=
begin
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ (hŒµ : 0 < Œµ), closed_ball x Œµ ‚äÜ u :=
    nhds_basis_closed_ball.mem_iff.1 hu,
  obtain ‚ü®R, Rpos, hR‚ü© : ‚àÉ (R : ‚Ñù), 0 < R ‚àß s ‚äÜ closed_ball 0 R := hs.subset_ball_lt 0 0,
  have : metric.closed_ball (0 : ùïú) (Œµ / R) ‚àà ùìù (0 : ùïú) :=
    closed_ball_mem_nhds _ (div_pos Œµpos Rpos),
  filter_upwards [this],
  assume r hr,
  simp only [image_add_left, singleton_add],
  assume y hy,
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ (z : E), z ‚àà s ‚àß r ‚Ä¢ z = -x + y, by simpa [mem_smul_set] using hy,
  have I : ‚à•r ‚Ä¢ z‚à• ‚â§ Œµ := calc
    ‚à•r ‚Ä¢ z‚à• = ‚à•r‚à• * ‚à•z‚à• : norm_smul _ _
    ... ‚â§ (Œµ / R) * R :
      mul_le_mul (mem_closed_ball_zero_iff.1 hr)
        (mem_closed_ball_zero_iff.1 (hR zs)) (norm_nonneg _) (div_pos Œµpos Rpos).le
    ... = Œµ : by field_simp [Rpos.ne'],
  have : y = x + r ‚Ä¢ z, by simp only [hz, add_neg_cancel_left],
  apply hŒµ,
  simpa only [this, dist_eq_norm, add_sub_cancel', mem_closed_ball] using I,
end

lemma set_smul_mem_nhds_zero {s : set E} (hs : s ‚àà ùìù (0 : E)) {c : ùïú} (hc : c ‚â† 0) :
  c ‚Ä¢ s ‚àà ùìù (0 : E) :=
begin
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ (Œµ : ‚Ñù) (H : 0 < Œµ), ball 0 Œµ ‚äÜ s := metric.mem_nhds_iff.1 hs,
  have : c ‚Ä¢ ball (0 : E) Œµ ‚àà ùìù (0 : E),
  { rw [smul_ball hc, smul_zero],
    exact ball_mem_nhds _ (mul_pos (by simpa using hc) Œµpos) },
  exact filter.mem_of_superset this ((set_smul_subset_set_smul_iff‚ÇÄ hc).2 hŒµ)
end

lemma set_smul_mem_nhds_zero_iff (s : set E) {c : ùïú} (hc : c ‚â† 0) :
  c ‚Ä¢ s ‚àà ùìù (0 : E) ‚Üî s ‚àà ùìù(0 : E) :=
begin
  refine ‚ü®Œª h, _, Œª h, set_smul_mem_nhds_zero h hc‚ü©,
  convert set_smul_mem_nhds_zero h (inv_ne_zero hc),
  rw [smul_smul, inv_mul_cancel hc, one_smul],
end

end semi_normed_space

section normed_space

variables {ùïú : Type*} [normed_field ùïú] {E : Type*} [normed_group E] [normed_space ùïú E]

theorem smul_closed_ball (c : ùïú) (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  c ‚Ä¢ closed_ball x r = closed_ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  rcases eq_or_ne c 0 with rfl|hc,
  { simp [hr, zero_smul_set, set.singleton_zero, ‚Üê nonempty_closed_ball] },
  { exact smul_closed_ball' hc x r }
end

end normed_space
